#!/usr/local/bin/zsh

TOKEN="$(git config --get user.token)"
EMAIL="$(git config --get user.email)"

[[ "${EMAIL}" == "s.white.1@elsevier.com" ]] && GITHUB_USERNAME="stuw-els"
[[ "${EMAIL}" == "stubillwhite@gmail.com" ]] && GITHUB_USERNAME="stubillwhite"

function list-all-pull-requests() {
    local fnam=$1

    local page=0
    local incomplete

    local queryUrl="https://api.github.com/search/issues?q=state%3Aopen+author%3A${GITHUB_USERNAME}+type%3Apr"

    local tmpfile=$(mktemp)

    echo > "${fnam}"
    echo '[' >> "${fnam}"
    while : ;
    do
      curl --silent -u "${EMAIL}:${TOKEN}" "${queryUrl}?per_page=100\&page=${page}" > "${tmpfile}"
      cat "${tmpfile}" >> "${fnam}"

      incomplete=$(cat "${tmpfile}" | jq '.incomplete_results')
      [[ "${incomplete}" == "false" ]] && break
      echo "," >> "${fnam}"
      page=$((page + 1))
    done
    echo ']' >> "${fnam}"
}

function pull-request-details() {
    local queryUrl=$1

    local reviewCount=$(curl --silent -u "${EMAIL}:${TOKEN}" "${queryUrl}/reviews" | jq --raw-output 'length')

    curl --silent -u "${EMAIL}:${TOKEN}" "${queryUrl}" \
        | jq --raw-output --arg reviews ${reviewCount} '[.head.repo.name, 
                                                         .state, 
                                                         (if ($reviews | tonumber) > 0 then "has-reviews" else "no-reviews" end), 
                                                         (if .comments > 0 or .review_comments > 0 then "has-comments" else "no-comments" end), 
                                                         .html_url, 
                                                         (.title | tojson) 
                                                        ] | @tsv'
}

# Highlight output using sed regex
# cat my-log.txt | highlight red ERROR | highlight yellow WARNING
function highlight() {
    if [[ $# -ne 2 ]] ; then
        echo 'Usage: highlight COLOR PATTERN'
        echo '  COLOR   The color to use (red, green, yellow, blue, magenta, cyan)'
        echo '  PATTERN The sed regular expression to match'
        return 1
    fi

    color=$1
    pattern=$2

    declare -A colors
    colors[red]="\033[0;31m"
    colors[green]="\033[0;32m"
    colors[yellow]="\033[0;33m"
    colors[blue]="\033[0;34m"
    colors[magenta]="\033[0;35m"
    colors[cyan]="\033[0;36m"
    colors[default]="\033[0m"

    colorOn=$(echo -e "${colors[$color]}")
    colorOff=$(echo -e "${colors[default]}")

    gsed -u s"/$pattern/$colorOn\0$colorOff/g"
}

if ([[ -z "${TOKEN}" ]] || [[ -z "${EMAIL}" ]] || [[ -z "${GITHUB_USERNAME}" ]]); then 
    echo 'Unable to find read token, email, and GitHub username'
    echo 'Check the output of:'
    echo '  git config --get user.token'
    echo '  git config --get user.email'
    return 1
fi

echo "Pull requests for ${GITHUB_USERNAME}:"

tmpfile=$(mktemp)

list-all-pull-requests "${tmpfile}"

while IFS=, read -rA url
do
    pull-request-details "${url}"
done < <(cat "${tmpfile}" | jq --raw-output '.[].items[].pull_request.url') \
    | column -t -s $'\t' \
    | highlight red no-reviews \
    | highlight green has-reviews \
    | highlight red has-comments
